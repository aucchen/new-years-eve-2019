title: Emily Chen
subtitle: Sitting alone in an alcove...
unavailable-subtitle: There's something else going on...
new-page: true
on-arrival: em_seen += 1; time += 1; energy -= 1; em_int += 1;
view-if: phase = 1 and em_seen = 0 and energy > 0 and time < p1_time
choose-if: has_interruption = 0
tags: phase1, emily, initiation

Emily is sitting by herself on a couch in a darkened alcove, her face faintly illuminated by the glow of the laptop screen. 

- @stare: Stare at Emily.


@stare
go-to: em_set_prev
set-jump: em_see_p1.em_talk

She somehow seems softer than when you last met. Her hair is tied up in a ponytail, her face framed by bangs with thick black-framed glasses. She's wearing a black and white striped sweater, like a zebra trying to blend in. Or something like that.

############################################


@em_talk

She glances up at you, and slowly waves.

"Are you going to sit down or just stand there?" Her voice is barely more than a whisper.


- @sit_next: Sit next to her.
- @stand: Stand there awkwardly.


@sit_next

"Um, sure..."

You sit down on the couch next to her.


- @what_reading: "What are you reading?"
- @how_are_you: "How have you been?"
- @sit_silence: Just sit there in silence.


@sit_silence

You just sit there. Emily switches between a few different windows on her computer, each one containing a wall of text and figures. Her eyes are seemingly focused on the screen.

- @what_reading: "What are you reading?"
- @how_are_you: "How have you been?"
- @stay_silent: Just sit there.
- @leave_emily: Leave her be.


@stand

"Um, I'll stand here awkwardly..."

Emily shrugs. "Okay. Suit yourself."

- @what_reading: "What are you reading?"
- @how_are_you: "How have you been?"
- @stand_silence: Just stand there.


@stand_silence

You stand there next to Emily, who is silent as her eyes are focused on the article on her computer screen.


- @what_reading: "What are you reading?"
- @how_are_you: "How have you been?"
- @stay_silent: Just stand there.
- @leave_emily: Leave her be.


######################################################


@what_reading

"Um, what are you reading?"

"Oh, um, I'm reading a few different things. Papers for my research mostly. And some, um, other things."

[? if anxiety >= 6 : Silence. She isn't going to talk if you aren't. ?]


# - @what_about: "What is it about?"
- @how_are_you: "How have you been?"
- @stay_silent: Don't say anything.
- @leave_emily: Just leave.


@what_about
on-arrival: anxiety += 1; em_relationship += 1

"So, um, what is it about?"

"It's a paper about the climate change and resource impacts of various dietary patterns." A pause. She glances at you. You turn away. "It's not directly part of my research, but still kind of interesting." She smiles to herself.

"Oh. Okay."

More silence. She's still staring at the screen, but isn't scrolling anymore.


- @how_are_you: "How have you been?"
- @stay_silent: Don't say anything.
- @leave_emily: Just leave.


@how_are_you

You're going to have to initiate this conversation. You hate initiating conversations. It's as if there is a physical block placed upon your brain, halting you from uttering a single word. But you can do it, right?

- @just_say_it: Just say "how are you" or something like that.
- @stay_silent: Actually, just stay quiet.


@just_say_it
on-arrival: time += 1; anxiety += 1; em_how_are_you = 1
go-to: em_not_okay if (prev_em = "date" or prev_em = "friends"); em_dodging if prev_em = "none"

"So, um, how have you been?"

"I'm doing fine," she says.


@em_not_okay
on-arrival: em_int += 1

# if you were friends with em, she's more likely to open up

"Oh... really?"

She sighs, and finally looks up from her laptop her laptop. "No. Of course not."

- @how_so: "How so?"
- @sorry_about_everything: "Sorry about everything..."


@how_so

"Um, how so?"

"Just, things kind of sucked with my parents recently. And college. And like, the state of the world in general I guess."

"Oh. Sorry."

"No, it's not your fault at all." Emily shakes her head. "So, enough about myself. How are you doing?"


- @doing_great: "I'm doing great!"
- @doing_alright: "I've been doing alright..."
- @doing_no_idea: "Um..."


@sorry_about_everything

"Um, sorry..."

"It's not your fault. Well, mostly not your fault. Actually, totally not your fault at all." She smiles(?) to herself. "So, enough about myself. How are you doing?" 


- @doing_great: "I'm doing great!"
- @doing_alright: "I've been doing alright..."
- @doing_no_idea: "Um..."


@em_dodging

# if starting relationship with em is bad

"Oh..." You aren't sure whether to prompt for more. She isn't exactly forthcoming. Thus, the silence resumes.

A long pause ensues, before Emily asks, "So, how are you doing?"

"Oh, um..." It takes a moment to get your bearings.


- @doing_great: "I'm doing great!"
- @doing_alright: "I've been doing alright..."
- @doing_no_idea: "Um..."



@stay_silent
go-to: em_asks if (prev_em = "date" or prev_em = "friends"); em_waiting if prev_em = "none"

You don't know what to say, and apparently Emily isn't in the mood for casual interaction. She doesn't seem so focused on the words on the screen anymore, her eyes occasionally glancing up, but still not looking at you.


#############################################################
# tell me about yourself...

@em_asks
on-arrival: em_int += 1; time += 1; em_how_are_you = 1

# if you were friends with em, she starts conversation with you

"So, how are you doing?", Emily asks, interrupting the silence.

"Oh, um..." It takes a moment to get your bearings.


- @doing_great: "I'm doing great!"
- @doing_alright: "I've been doing alright..."
- @doing_no_idea: "Um..."


@doing_great

"Um, I've been doing great!"

"Oh? How so?"


- @lots_of_friends: (lie) "I've made lots of friends!"
- @doing_well_in_class: (lie) "I'm doing well in my classes!"
- @doing_research: "I've been doing research..."
- @grad_school: "I've applied to grad school!"
- @missed_you: "I've really missed you..."


@doing_alright

"Um, I've been doing alright..."

"Oh? What's been happening?"


- @tough: "It's been tough..."
- @doing_research: "I've been doing research..."
- @grad_school: "I've applied to grad school!"
- @missed_you: "I've really missed you..."


@doing_no_idea

"Um..." You aren't sure what to say. You never know what to say.


- @tough: "It's been tough..."
- @doing_research: "I've been doing research..."
- @grad_school: "I've applied to grad school!"
- @missed_you: "I've really missed you..."


@lots_of_friends
view-if: prev_em = "date" or prev_em = "friends"
on-arrival: despair += 1

Are you really going to lie like that? What's the point? What would you be trying to prove? You're not doing great, and Emily can probably tell.

"Um, I'm making a lot of friends! Haha."

"Oh. Haha."

"That was a joke."

"Yeah?"

"Yeah."

"Oh. Sorry."

"No, it's not your fault... well, mostly not your fault."


- @doing_research: "I've been doing research..."
- @grad_school: "I've applied to grad school!"
- @missed_you: "I've really missed you..."


@doing_well_in_class
view-if: prev_em = "date" or prev_em = "friends"
on-arrival: despair += 1

"Um, I'm doing well in class! I mean, sort of, not really."

"Oh. Sorry."

"No, it's not your fault... well, mostly not your fault."


- @doing_research: "I've been doing research..."
- @grad_school: "I've applied to grad school!"
- @missed_you: "I've really missed you..."


@doing_research
go-to: lull

"Um, I've been doing research..."

"Yeah? What about?"

"Um, it's in the biology department, about using genome-I mean epigenome-sequencing to assess development."

"Oh. That's cool. I've been kind of involved in climate science research at my college, but it's such a small school so it's kind of hard."

"That's great..."



@tough
go-to: lull

"Um, it's been tough..."

"Yeah? How so?"

"Um, just like... classes and everything, not enough time to do everything you want, having to make so many decisions and worry about the future, and, um... being alone all the time but still surrounded by people. Sorry, does that make any sense?" 

"Oh. Sorry. Yeah, I kind of get that."

"Thanks..."



@grad_school
go-to: lull

"Um, I've applied to grad schools..."

"Yeah? What for? Where to?"

"Um, molecular biology and genomics and stuff. And it's a whole bunch of schools..."

"Oh. That's cool. I'm applying to grad schools too, in climate science."

"That's great..."


@lull

A lull in the conversation. Emily is quiet, and you don't know what to say next.


- @leave_emily: "I'll be going..."
- @missed_you: "I've missed you..."


@missed_you
view-if: prev_em = "date" or prev_em = "friends"
on-arrival: em_int += 1; energy -= 1; em_relationship += 1

"Um, I've missed you a lot... sorry..."

"No, it's my fault too." Emily glances at you. "I've missed you too. It's been such a long time..."

Okay. Was your statement supposed to have *romantic* undertones?

- @yes_romantic: Yes...
- @hope_romantic: Hopefully?...
- @no_romantic: No...
- @idk_romantic: I don't know...


@yes_romantic
go-to: hope_romantic

@hope_romantic
on-arrival: em_rom += 1

In a movie, this would be the moment where you hugged, kissed, or something like that. But that will never happen. The conversation has suddenly acquired an awkward quality. 

- @romanticize: Romanticize.
- @exit_moment: You're going to have to exit this moment.
- @stay_with_emily: Actually, you'd rather stay here.


@romanticize
on-arrival: em_romanticize += 1; em_rom += 1

Emily gazes at her computer screen with a downcast look, through unfocused half-open eyes and thick black-framed glasses. Her mouth is slightly open, her expression as if she's deep in thought. Her long and slender fingers move slowly across the keyboard.

There's something about her you can't ignore. Something that makes you want to imagine a future with her that stretches out forever.

# You like Emily because she has never asked anything of you. Because she has never told you to do anything.

"What are you staring at?", she says, glancing at you. You turn away.

"No, I mean... sorry."

"It's fine. There's nothing wrong with staring."

- @exit_moment: You're going to have to exit this moment.
- @stay_with_emily: Actually, you'd rather stay here.


@no_romantic
go-to: idk_romantic

@idk_romantic

Okay then.

Anyway, regardless of your *feelings* or lack thereof, the conversation has suddenly acquired an awkward quality. As much as you would like to stay...


- @exit_moment: You're going to have to exit this moment.
- @stay_with_emily: Actually, you'd rather stay here.
- @say_something_2: Say something?


@say_something_2

What are you going to say?

- @mind_empty: Your mind blanks.


@mind_empty

Ugh. You can't think of anything to say, as usual.

- @exit_moment: Leave.
- @stay_with_emily: Stay here, silent.


@exit_moment
on-arrival: time += 1; energy -= 1; despair += 1

"Um, I'm going to...", you begin, before getting up. Something doesn't feel right.

"See you later?" She waves as you return to the living room. You feel a bit more lonely.


@stay_with_emily
view-if: em_int >= 2
on-arrival: time += 1; em_int += 1

You feel as if you are overstepping things a little by sitting here on the couch with her. But she seems fine with it. Or does she?

Neither of you talk at all. But it's fine; it's a comfortable silence, right? Is that what they call it?

- @stay_more: Just sit here.
- @em_hw: Do homework?
- @go_back: Do something else.
- @phase_2


@stay_more
view-if: time < p1_time
on-arrival: time += 1

For some reason you enjoy staying here. Emily taps on her keyboard, her eyes glued to the screen. She doesn't bother you, and you don't bother her. Meanwhile you observe the dynamics of the party.

- @stay_more: Just sit here.
- @em_hw: Do homework?
- @go_back: Do something else.
- @phase_2


@em_hw
view-if: time < p1_time
on-arrival: time += 1

Technically it's not homework. It's just a bunch of readings for your research project, some books your supervisor suggested that might or might not be actually helpful for the next phase of your project.

You take out your biological sequence analysis textbook and attempt to read.

- @em_hw_2: It's hard to concentrate.

@em_hw_2

Somehow, the words and diagrams and equations fail to reach your brain. You see each word individually, each sentence even, but only as scribbles on the page, not as any signifiers of meaning. Still, you attempt to skim over each page, gleaning whatever semblance of meaning you can.

"It looks interesting," Emily says. She's staring at your textbook.

- @em_hw_int: "Yeah, it is."
- @em_hw_not_int: "Not really."


@em_hw_int

"Um, yeah, it sure is. It's interesting."

- @em_hw_3: Keep trying to read.
- @go_back: Do something else.
- @phase_2


@em_hw_not_int

"Um, not really. It's not that interesting."

"Oh, that's too bad."

- @em_hw_3: Keep trying to read.
- @go_back: Do something else.
- @phase_2


@em_hw_3
on-arrival: time += 1

You do not succeed in reading more.

"Can you tell me about, uh, probabilistic motif detection algorithms?", Emily asks. She's still intently staring at your book (why is she so interested in this?).

- @um_no: "Um, no..."


@um_no

"Um, no. I can't. I actually, um... don't know anything."

Emily smiles. "Yeah, it's hard to do work at a social gathering. I've barely read anything at all."

She closes her laptop. "Alright, I'm gonna walk around a bit." She glances at you, and walks away.

- @follow: Follow her?
- @root: Do something else.


@follow

That would be awkward, wouldn't it? You should just... do whatever it is you were going to do at the party.


@go_back

"Um, I kind of want to do something else..."

"Sure, go ahead," Emily says. "See you later."

You make your way back to the living room.


@em_waiting

Emily is not going to initiate the conversation. Do you want to say something, or just go?

- @how_are_you: "How are you?"
- @leave_emily: Just leave.


@leave_emily
on-arrival: energy -= 1; despair += 1; time += 1

Somehow, you sense that you are a little unwelcome here, as if you have overstayed the bounds of acceptable social discourse given your previous relationship or lack of thereof.

"Um, see you later..." You wave at her and make your way back to the living room. She waves back.

You feel just a little bit more lonely.
